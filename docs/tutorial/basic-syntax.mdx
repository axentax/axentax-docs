---
sidebar_position: 1
---

# 基本的な記載方法

## Axentaxの基本

ここではAxentaxで音を鳴らすための基本的なコードの記載方法について学びます。\
[Playground](https://axentax.github.io/axentax-playground/)で実際のコードを試しながら学ぶことができます。

### 基本的な記載方法

Axentaxでは、`@@`から始まる行にコードを記載します。\
`@@`の後に続く内容は、Axentaxのフィンガリングを指定するためのものです。

コード指定を含むフィンガリングの指定は`@@ { }`の中に記述します。

> この`{}`囲いのことを`block`と呼びます。

```
@@ {
  C
}
```

blockは入れ子にできます。

```
@@ {
  C
  {{{ D }}}
}
```

複数ギター同時演奏（３ブロックまで可能）

```
@@ {
  C
} >> {
  Am
} >> {
  Em
}
```

> @@ から始まるひとかたまりをregionと呼びます。\
> 上記コードは１つの`region`に、3つの`block`が記載されていることになります。

<a id="bpm"></a>

## BPM指定

BPMを`@@`の次に記載することで曲のスピードを変更できます。

```
@@ 180 {
  C C C C
}
@@ 240 {
  C C C C
}
```
上記の例では、最初のblockは180BPMで、次のblockは240BPMで演奏されます。

:::note BPMについて
BPMはリージョン内の1つ目のblockのみに指定可能です。
:::

:::warning BPMは次のregionでも引き継がれます。
BPMはregionを跨いでも初期化されず、次のregionでも引き継がれます。\
BPMを変更したい場合は、次のregionで再度指定してください。
:::

:::tip 動的BPM
🚀 [動的にBPMを遷移させることも可能です](/docs/tutorial/special-syntax#dynamic-bpm)
:::

## 付割指定

block propに記載した`1/4`のような分数表記は音の長さとなります。

```
@@ 180 1/4 {
  C C C C
}
```

## 変速チューニングと弦数の指定

block propにチューニングを記載すると、そのblockは指定されたチューニングに従います。

```
@@ D|A|D|G|A|D {
  C D E
}
```

チューニングは block毎に設定する必要があります。

```
@@ D|A|D|G|A|D {
  // ここは DADGAD
} >> {
  // ここは EADGBE
} >> D|A|D|G|A|D {
  // ここは DADGAD
}
```

9弦まで指定できます。

```
@@ D|G|C|F|A#|D#|G#|C|F {
  0|0|0|0|0|0|0|0|0
}
```

### チューニング最高低

- デフォルト6弦チューニング: `E|A|D|G|B|E`
- 最低音の9弦チューニング: `D|G|C|F|A#|D#|G#|C|F`
- 最高音の9弦チューニング: `C#|F#|B|E|A|D|G|B|E`
- チューニングより多い弦数のフィンガリングはできません。
  弦数を増やしたフィンガリングをする場合は、そのブロックで使いたい弦数のチューニングを実施してください。

## 基本記法

コードやTabで記載した音をnoteと呼びます。

### コード記法

コードの表記はこれまで記載したようにアルファベット表記することができます。\

```
@@ {
  C Dm Em7 Fmaj7 Gdim
}
```

:::tip コード記法では使用弦やフレットポジションを指定できません
明示的に使用弦やフレットを指定したい場合、後述するTab記法を使用します。
:::

### Tab記法

`|`を使ってフレット指定をすることができます。Tab譜面を横にしたような表現です。\
左が低音弦で右が高音源となります。

```
@@ {
  2|2|2|0|0|0
  5|2|2|5|0|0
}
```

記載以降フレット指定しない場合`|`は省略できますが
6弦だけ記載する場合も必ず１つは`|`の表記が必要です。

```
// 不正な記載
@@ {
  2 3 4 // `|`がないとエラーになります
}

// 正常な記載
@@ {
  2|
  |2
  ||2
  |||2
  ||||2
  |||||2
}
```

<a id="tone-length"></a>

## 音の長さ

音の長さを接尾辞で調整できます。\
>[スタイルによる音長](/docs/tutorial/craft-syntax#tone-length)でも音の長さを指定できます。

```
@@ {
  C~~~ // "~"をフィンガリングの末尾に記載（4倍の長さ）
  C^   // "^"をフィンガリングの末尾に記載（半分の長さ）
  C=   // "^"をフィンガリングの末尾に記載（3分の1の長さ）
}
```

以下の各行は同じ音長となります。

```
@@ {
  C
  C= C= C=
  C^ C^
}
```

複数の接尾辞をつけることができますが、付割の制御が難しくなる傾向にあります。\
※スタイルでも音長の調整ができます。詳しくは「スタイル構文」参照してください。

```
@@ {
  C== C== C== C~~
}
```

:::warning ブロックに対して`{}~~`のような記載はできません
:::

## 休符

`r`で休符を表現します

```
@@ {
  C r D r
}
```

休符の長さも接尾辞(^)で調整できます

```
@@ {
  C r^ C r^
}
```

## 前の音を残す

前の痕を残しつつ次の音を鳴らしたい場合には先頭に`..`を記載します。\
以下例では、最初に弾いた 6弦と5弦の音を残響したまま、4弦を順に鳴らします。

```
@@ {
  0|2|2
  ..||3
  ..||5
  ..||7
}
```

コードでも記載可能です。

```
@@ {
  Em7
  ..||3
  ..||5
  ..||7
}
```
:::warning コード自体には使用できません。
..Em // NG
:::

## 音種

### ミュート奏法

ミュート奏法は`:m`や`:M`で指定します。

```
@@ 1/8 140 {
  0|2~ 0|:m 0|:m 0|:m 0|:m 0|:m 0|:m
}
```

`:M`で指定した場合、前の音を残すと併用しハーフミュート効果を出すことができます。

```
@@ 1/4 140 {
  0|:m
  |2:m
  ||||0:M
  ||2:M
  |||||0:M
  ||||0:M
  |2:M
  ||||0:M
}

```

### ブラッシング奏法

ブラッシング奏法は`:d`、`:D`、`:u`、`:U`で指定します。

- `:d` : ダウン
- `:D` : ダウン(強)
- `:u` : アップ
- `:U` : アップ(強)
となります。

```
@@ 120 1/8{
  0|0|0|0|0:D
  0|0|0|0|0:u
  0|0|0|0|0:d
  0|0|0|0|0:u
}
```

## コメントアウト

### //記法

`//`でコメントアウトできます。

```
@@ {
  C // ここはコメントアウト
}
```

### /**/記法

`/* */`でもコメントアウトできます。

```
@@ {
  C /* ここはコメントアウト */
}
```

### 一括コメントアウト

`__end__`を記載すると、以降の指定は行末までコメントアウト扱いになります。

```
@@ {
  // ブロックの中間に`__end__`を記載すると
  // 括弧の解決ができなくなりエラーとなります
}

__end__
// この行以降は全て無効
@@ {

}
```
