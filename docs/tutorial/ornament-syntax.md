---
sidebar_position: 3
---

# 装飾的な奏法

ここではノイズ、スライド、チョーキング、ビブラート奏法など装飾的な奏法を学びます。\
[Playground](https://axentax.github.io/axentax-playground/)実際のコードを試してみましょう。

## 弦ノイズを鳴らす

弾き初めに弦ノイズを鳴らすには、音指定の直前に'/'を記載します。\
> ※Axentaxでは、弾き初めに弦ノイズを鳴らすことを`ストラム`と呼んでいます（ジャカジャカ弾くストラム奏法とは分けて考えます）

```
@@ 1/16 {
  /|||||2 r |||||3 r:1/4 /|||||2~~~
}
```

:::warning Tab記法のみ対応しています。
:::

## スライド

フレットとフレットの間を滑らかに繋ぐように移動する奏法です。\
Axentaxでは`:to`を使用してスライドを実現します。

### 基本的なスライド記法

以下のようにスライドを開始するTabのスタイルに`:to`を指定します。

```
@@ 1/4 {
  2|2:to 12|12|12 // スライドで移動
}
```

:::warning 開始フレットから着地フレットまでが近すぎると効果が感じられない場合があります。
:::

### 着地でピッキングしない

`:to&`のようにtoの後に`&`をつけると着地音でピッキングしない程度の音量に自動調整します。\

```
@@ {
  |||||2:1/2:to&(3/4) // 着地点でピッキングしない
  |||||12:1/2:to(1/4) // 着地点でピッキングする
  /|||||2~~~
}
```
:::note 4弦から4弦など同じ弦上での移動のみ効果が適用されます。
:::

:::warning 弦ノイズを鳴らした場合などギターの特性上適用できないケースがあります。
:::

### スライド開始点を変更する

`:to(1/4)`のように、指定された音長をさらに分割した場所がスライド開始点となります。\
ここで指定している`1/4`は4分音符ではなく、音を`4`分割して`分割された1つ目の末端`からスライドが始まる。と覚えてください。\
開始点の指定概念は少し混乱するかもしれませんが覚えてしまうと直感的な指定ができるよう設計されています。\

```
@@ 1/4 {
  // 以下の場合、`7|~`はふたつとも2分音符
  7|~:to(1/4) 0|2 // 2分音符を4分割した一つ目の末端からスライド開始
  7|~:to(1/16) 0|2 // 2分音符を16分割した一つ目の末端からスライド開始
}
```

:::note `:to`のように何も指定しない場合、システムが自動的にスライド開始位置を決定します。
:::

:::tip `:to(1/2)`とすると、元々の音の長さに関わらず、常に真ん中からスライド開始します。
:::

:::warning `:to(1/1)`とすると、音の末端が開始地点になってしまいスライド効果は無くなります。
:::

### スライド開始速度の変更

スライドで最初の移動を速くするか、後半を速くするか変更できます。\
何も指定しない場合、最初の移動が速い状態ですが、以下のように`!slow`を指定すると最初は遅い移動となります。
また、`!mid`を指定すると均等な速度で移動します。

> fastの場合徐々に遅くなり\
> slowの場合徐々に速くなる\
> デフォルトはmid(一定)

```
@@ {
  |||2:to(2/4!slow) |||12
  |||2:to(2/4!mid) |||12
  |||2:to(2/4!fast) |||12 // !fastは指定しない場合とほぼ同じです。
  |||2:to(2/4) |||12
}
```

<!--
開始速度を0~100で指定できるが（デフォルト25）効果が視覚的に感じないので一旦コメントアウト

```
@@ 30 {
  |||||2:to(3/4!fast.100) |||||12
  |||||2:to(3/4!fast.0) |||||12
}
```
-->

### 着地点を無視する

`:to(hi)`や`:to(low)`とすることで着地点に関係なく、ハイポジション/ローポジションに向かってスライドします。

```
@@ 1/2 {
  7|:to(hi) 0|2
  7|:to(hi!1/4) 0|2
}
```

`hi`や`low`の後に`.フレット番号`を入れると、そのフレットに向かう意識でスライド開始します。\
`遠いほど早く移動し始める`ことをシミュレートできます。

```
@@ 1/4 {
  7|:to(hi.24)
}
```

## ベンド

`:bd(..)`を指定することでチョーキングやアームダウン奏法を表現できます。\
以下一例は、音の始まりから終わりにかけて2音チョーキングします。

```
@@ {
  ||2:1/4:bd(.. 2)
}
```
:::info この記載は簡易指定方法です。詳細指定は後述します。
:::

### ベンド基本構文

```:bd(ベンド位置指定 ベンド量指定 vib ast)```

- 各項目は空白で区切り指定します。
- ベンドの位置は音の長さが**全音符を前提**として算出します。
- ベンド量は、2音下から2音上まで少数指定できます。
- vibを指定するとビブラート（波曲線）になります
- 通常はサイクロイド曲線で音程を自動操作しますが、astを指定するとベンド/ビブラート曲線がアステロイド曲線(星形曲線)になります（出だし緩やかで折り返しが急となります）

### ベンド位置指定の基本仕様

ベンド位置指定は
`開始位置..終了位置/分割分音符`のように指定します。\
後述しますが、組み合わせ次第で各項目を省略できます。

:::info ベンド位置指定の仕様
どのようなベンド指定をしても、**位置決定の基準は全音符**となります。\
音の長さ自体が全音符に満たない場合でも全音符を前提に位置決定されます。\
あまりケースはないと思いますが、元の音の長さが全音符四つ分の**全体に適用**する場合`0..4/1`のように末端指定となる分母の方が大きくなります。
:::

### 分割分音符と省略記法

分割数分音符とは、開始終了の位置を決定する基準となります。\
例えば、分割数分音符に`4`を指定した以下のような場合の末端は`1`となります。

PlaygroundのNOTE LABOを確認しながら指定するとわかりやすいです。

```
@@ {
  |||||2:1/4:bd(0..1/4 2)
}
```

**分割分音符**を省略した`0..2`の場合、暗黙的に`/16`が指定され`0/16`の位置から`2/16`の位置までの間でベンド量が適用されます。

### 開始位置と省略記法

開始位置も分割数分音符が基準となります。\
以下の場合、元の音長が全音符`1/1`なので、開始位置`8`は音の中間位置からとなりますので、中間から末端にかけて`2'のチョーキングが適用されます。

```
@@ {
  |||||2:1/1:bd(8..16/16 2)
  |||||2:1/1:bd(8../16 2) // 省略記載
}
```

**開始位置**を省略した`..4/8`のような表記の場合、開始位置は`0`が自動適用されます。

### 終了位置と省略

終了位置も同様に分割数分音符が基準となります。\
以下の場合、元の音長が全音符`1/1`なので、終了位置`8`は音の中間位置からとなりますので、先頭から中間にかけて`2'のチョーキングが適用されます。

```
@@ {
  |||||2:1/1:bd(0..8/16 2)
  |||||2:1/1:bd(..8/16 2) // 省略記載
}
```

**開始位置**を省略した`4../8`のような表記の場合、終了位置は**音の末端**が自動適用されます。\
**開始位置**と**終了位置**を省略した`..`を指定すると`音の最初`から`音の末端`までにかけてが自動適用されます

### 開始終了位置をどちらも省略 ###

```
@@ {
  ||2:1/4:bd(.. 2)
}
```

### 開始終了位置とベンド量全て省略

ベンド量を省略した場合、ベンド量は`1`が自動適用されます。

```
@@ {
  |||||2:1/4:bd(0..1/4 1) // 省略なし
  |||||2:1/4:bd(..) // 全て省略
}
```

## ビブラート

ビブラート（音を揺らす）の指定も基本はベンドと同様に`:bd`を使用します。\
ビブラートとの指定方法の違いは`vib`を付与することです。

```
@@ {
  ||2:1/1:bd(../8 1 vib)
  ||2:1/1:bd(vib) // 省略した場合、一つ前と同じ設定となる
  ||2:1/1:bd(../8 2 vib) // ビブラート幅を2音
  ||2:1/1:bd(0..4/16 2 vib) // 前半に少し速いビブラート
  ||2:1/1:bd(0..8/32 2 vib) // 前半にとても速いビブラート
  ||2:1/1:bd(24..32/32 2 vib) // 後半にとても速いビブラート
}
```

## ベンドの応用

チョーキングやビブラートを一つの音に対して複数指定できます。\
具体的には以下のように`,`区切りで指定することによって複数適用されます。

以下の例では、全音符`1/1`の音を`/8`で8分割して\
2種類のチョーキングと1つのビブラートをかけています。

サンプルコードを実行すると以下のような波形になります。

![ビブラート波形](/img/axe/bd-sample.png)\
波形は[Playground](https://axentax.github.io/axentax-playground/)のNOTE LABOで確認できます。

```
@@ {
  ||7:1/1:bd(
    0..2/8 1
    2..6 2 vib
    6.. -2
  )
}
```

:::tip
複数記載する場合、**分割分音符**は最初の一度のみの記載で、`:bd`内の次の指定に引き継がれます。\
全て記載することも可能ですが、一つの`:bd`内で**分割分音符**を変更することはできません。
:::

## レガート(プリング/ハンマリング) [非推奨]

同弦で基音から4フレット以内でのみ`:leg`によってレガートを適用できます。\
これはハンマリングオン/プリングオフをシミュレートする場合に適切です。

`:leg`は後のnoteに記載します。

```
@@ 1/4 160 {
  |||||2
  |||||4:leg
}

@@ 1/4 160 {
  |||||2
  |||||4:leg
  |||||2:leg
  |||||5:leg
  |||||0
  |||||3:leg
}
```

:::warning チョーキングやビブラートと同時に使用することはできません
:::


:::warning システム仕様で注意すべき点
  レガート機能は、レガート処理が必要となる並びを一つのグループとして処理します。\
  この場合、そのグループ内で5フレット以上（5音以上）の移動があると\
  グループ全体のレガート処理を中止し、音量のみレガートに似せて処理します。\
  
  以下のような場合、`|||||0`に`:leg`は指定せず、`|||||5:leg`で一旦グループ処理が終わるように指定してください。
  
  ```
  @@ 1/4 160 {
    |||||2
    |||||4:leg
    |||||2:leg
    |||||5:leg
    |||||0:leg // ここでlegを指定してしまうと全体的にleg効果が無効になります
    |||||3:leg
  }
  ```
:::
